buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm' version '1.4.30-M1'
    id 'idea'
    id 'maven-publish'
    id 'signing'
    id("io.github.gradle-nexus.publish-plugin") version "1.0.0"
}

group 'xyz.poeschl'
sourceCompatibility = JavaVersion.VERSION_11

compileKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_11
}
compileTestKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_11
}

javadoc {
    options.addBooleanOption('html5', true)
}

test {
    useJUnitPlatform()

    testLogging {
        events "skipped", "failed"
        showStandardStreams true
    }

    reports {
        html.enabled = true
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

signing {
    def signingKey = System.getenv('SIGN_KEY')
    def signingPassword = System.getenv('SIGN_PASSWORD')
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications
}

publishing {
    publications {
        kixelflut(MavenPublication) {
            from components.java

            groupId project.group
            artifactId project.name
            version project.version

            artifact sourcesJar
            artifact javadocJar

            pom {
                name = project.name
                description = 'A Pixelflut interface for Java and Kotlin written in Kotlin.'
                url = 'https://github.com/Poeschl/Kixelflut'

                licenses {
                    license {
                        name = 'Apache-2.0 Licence'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'Poeschl'
                        name = 'Markus PÃ¶schl'
                        url = 'https://poeschl.xyz/'
                    }
                }

                scm {
                    url = "https://github.com/Poeschl/Kixelflut"
                }
            }

            versionMapping {
                allVariants {
                    fromResolutionResult()
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username = System.getenv('OSSRH_USER')
            password = System.getenv('OSSRH_PASSWORD')
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'io.github.microutils:kotlin-logging-jvm:2.0.4'

    testImplementation "org.jetbrains.kotlin:kotlin-test"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junit_version"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:$junit_version"
    testImplementation "org.assertj:assertj-core:3.18.1"
    testImplementation "io.mockk:mockk:1.10.3"
    testImplementation 'org.slf4j:slf4j-simple:1.7.30'
}
