buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
}

plugins {
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm' version '1.4.30-M1'
    id 'idea'
    id "com.jfrog.bintray" version "1.8.5"
    id "maven-publish"
}

group 'io.github.poeschl'
sourceCompatibility = JavaVersion.VERSION_11

compileKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_11
}
compileTestKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_11
}

test {
    useJUnitPlatform()

    testLogging {
        events "skipped", "failed"
        showStandardStreams true
    }

    reports {
        html.enabled = true
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def additionalPomInfo = {
    licenses {
        license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }
    developers {
        developer {
            id 'poeschl'
            name 'Markus PÃ¶schl'
            url 'https://poeschl.xyz/'
        }
    }
    scm {
        url "https://github.com/Poeschl/Kixelflut"
    }
}

publishing {
    publications {
        bintrayPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId project.group
            artifactId project.name
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('name', "$project.name")
                root.appendNode('url', 'https://github.com/Poeschl/Kixelflut')
                root.children().last() + additionalPomInfo

                def dependenciesNode = asNode().getAt('dependencies')[0] ?: asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies, adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['bintrayPublication']
    publish = false //set this to true to auto-publish

    pkg {
        repo = 'maven'
        name = project.name
        licenses = ['Apache-2.0']
        issueTrackerUrl = 'https://github.com/Poeschl/Kixelflut/issues'
        vcsUrl = 'https://github.com/Poeschl/Kixelflut.git'
        githubRepo = 'Poeschl/Kixelflut'
        version {
            name = project.version
            desc = "Version ${project.version}"
            released  = new Date()
            mavenCentralSync {
                sync = false
            }
        }
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.BIN
    gradleVersion = '6.7.1'
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'io.github.microutils:kotlin-logging-jvm:2.0.4'

    testImplementation "org.jetbrains.kotlin:kotlin-test"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junit_version"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:$junit_version"
    testImplementation "org.assertj:assertj-core:3.18.1"
    testImplementation "io.mockk:mockk:1.10.3"
    testImplementation 'org.slf4j:slf4j-simple:1.7.30'


}
